static Project findRootVersioningProject(Project proj) {
    if (proj.path.contains('addons:')) {
        return proj.rootProject.findProject(':addons')
    } else if (proj.path.contains(':ime')) {
        return proj.rootProject.findProject(':ime')
    } else {
        throw new IllegalStateException("Can not find versioning root for project ${proj.path}")
    }
}

android.defaultConfig {
    def versioningProject = findRootVersioningProject(project)
    def versionData = versioningProject.autoVersioning.versionData
    def forcedVersionCode =
            project.ext.has('override_version_code') ? project.ext.override_version_code : null
    def forcedVersionName =
            project.ext.has('override_version_name') ? project.ext.override_version_name : null

    if (forcedVersionCode != null) {
        versionCode forcedVersionCode as Integer
    } else {
        versionCode versionData.versionCode
    }

    if (forcedVersionName != null) {
        versionName forcedVersionName.toString()
    } else {
        versionName versionData.versionName
    }

    println "Building ${project.path} ${versionName} (or ${project.version}), code ${versionCode}."
}
